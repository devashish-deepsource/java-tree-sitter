plugins {
    id "java-library"
}

group = "ai.serenade"
version = "1.1.1"

repositories {
    mavenLocal()
    mavenCentral()
}

task buildSharedObject {
    inputs.file "lib/ai_serenade_treesitter_Languages.cc"
    inputs.file "lib/ai_serenade_treesitter_Languages.h"
    inputs.file "lib/ai_serenade_treesitter_TreeSitter.cc"
    inputs.file "lib/ai_serenade_treesitter_TreeSitter.h"
    outputs.file "libjava-tree-sitter.so"

    doLast {
        exec {
            environment "JAVA_HOME", "/usr/lib/jvm/java-11-openjdk-amd64"
            commandLine "./build.py", "libjava-tree-sitter", "tree-sitter-java"
        }
    }
}

clean {
    delete "libjava-tree-sitter.so"
}

compileJava {
    options.compilerArgs += ["-h", "${project.projectDir.toString()}/lib"]
}

compileTestJava {
    dependsOn buildSharedObject
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    environment "JAVA_TREE_SITTER", "${project.projectDir.toString()}/libjava-tree-sitter.so"
}
