plugins {
    id "java-library"
    id "maven-publish"
}

group = "ai.serenade"
version = "1.1.1"

repositories {
    mavenLocal()
    mavenCentral()
}

task buildSharedObject {
    inputs.file "lib/ai_serenade_treesitter_Languages.cc"
    inputs.file "lib/ai_serenade_treesitter_Languages.h"
    inputs.file "lib/ai_serenade_treesitter_TreeSitter.cc"
    inputs.file "lib/ai_serenade_treesitter_TreeSitter.h"
    outputs.file "libjava-tree-sitter.so"

    doLast {
        def env = [
                JAVA_HOME: System.getenv("JAVA_HOME"),
                MACOSX_DEPLOYMENT_TARGET: "12"
        ]
        exec {

            environment << env
            commandLine "./build.py", "tree-sitter-java"
        }
    }
}

clean {
    delete "libjava-tree-sitter.so"
}

compileJava {
    options.compilerArgs += ["-h", "${project.projectDir.toString()}/lib"]
}

compileTestJava {
    dependsOn buildSharedObject
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    String resPath = (System.getProperty("os.name").contains("Mac")) ? "${project.projectDir.toString()}/libjava-tree-sitter.dylib" : "${project.projectDir.toString()}/libjava-tree-sitter.so"
    println(resPath)
    environment "JAVA_TREE_SITTER", resPath
}
